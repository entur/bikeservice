buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
        classpath "org.owasp:dependency-check-gradle:$owasp_dependency_check_version"
        classpath "io.ktor:ktor-client-core:$ktor_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'org.owasp.dependencycheck'

group 'org.entur'
version '0.0.1'
mainClassName = "io.ktor.server.jetty.EngineMain"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

dependencyCheck {
    failBuildOnCVSS = 0 // Specifies if the build should fail for a CVSS score equal to or above the specified level.
    suppressionFiles = ["$projectDir/dependencycheck-base-suppression.xml"]
}

jar {
    manifest {
        attributes 'Main-Class': 'org.entur.mobility.bikes.ApplicationKt'
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenLocal()
    jcenter()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "io.micrometer:micrometer-registry-prometheus:$micrometer_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "net.logstash.logback:logstash-logback-encoder:6.3"
    implementation "io.ktor:ktor-server-jetty:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-metrics-micrometer:$ktor_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task updateDocs{
    doLast {
        exec {
            workingDir './docs'
            commandLine 'gsutil', 'cp', 'bikeservice.mdx', 'gs://entur-docs.appspot.com/mobility/docs'

        }
    }
}
